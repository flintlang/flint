contract FlightManager {
  var flightInformation: FlightInformation
  let admin: Address
  var ticketPrice: Int
  
  var numRemainingSeats: Int

  var passengers: [Address] = []
  var numPassengers: Int = 0

  var amountPaid: [Address: Wei] = [:]
}

FlightManager :: caller <- (any) {
  public init(flightID: String, 
              admin: Address, 
              ticketPrice: Int, 
              numSeats: Int) {
    self.flightInformation = FlightInformation(flightID)
    self.admin = admin
    self.ticketPrice = ticketPrice
    self.numRemainingSeats = numSeats
  }

  @payable
  mutating public func buy(implicit value: Wei) {
    let amountGiven: Int = value.getRawValue()
    assert(amountGiven == ticketPrice)
    assert(numRemainingSeats > 0)
    assert(flightInformation.isCancelled == false)

    // Record the received Ether in the contract's state.
    amountPaid[caller].transfer(&value)

    passengers[numPassengers] = caller
    numPassengers += 1

    numRemainingSeats -= 1
  }

  public func getFlightID() -> String {
    return flightInformation.flightID
  }

  public func isFlightCancelled() -> Bool {
    return flightInformation.isCancelled
  }

  public func getTicketPrice() -> Int {
    return ticketPrice
  }

  public func getNumRemainingSeats() -> Int {
    return numRemainingSeats
  }
}

FlightManager :: (admin) {
  mutating public func cancelFlight() {
    flightInformation.cancelFlight()
  }
}

FlightManager :: passenger <- (passengers) {
  public func retrieveRefund() {
    assert(flightInformation.isCancelled)
    send(passenger, &amountPaid[passenger])
  }
}

struct FlightInformation {
  var flightID: String
  var isCancelled: Bool = false

  init(flightID: String) {
    self.flightID = flightID
  }

  mutating func cancelFlight() {
    isCancelled = true
  }
}
