// ----------------------------------------------------------------------------
// Token using Flint features to add Minting
// ----------------------------------------------------------------------------

contract FlintToken (Minting, MintingFinished) {
    var balances: [Address: Int]
    var allowed: [Address: [Address: Int]]
    var totalSupply: Int = 0
    var owner: Address

    event Transfer {
      let from: Address = 0x00000_00000_00000_00000_00000_00000_00000_00000
      let to: Address
      let value: Int
    }
    event Approval {
      let from: Address
      let to: Address
      let value: Int
    }

    event Mint {
      let to: Address
      let value: Int
    }

    event MintFinished {}
}

FlintToken @(any) :: caller <- (any) {
  public init() {
    self.owner = caller
    self.balances = [:]
    self.allowed = [:]
    become Minting
  }

  // Transfer token for a specified address
  // - param to: The address to transfer to.
  // - param value: The amount to be transferred.
  // - return: true or is reverted
  public func transfer(to: Address, value: Int) -> Bool
    mutates (balances)
  {
    assert(balances[caller] >= value)
    assert(to != 0x00000_00000_00000_00000_00000_00000_00000_00000)

    balances[caller] -= value
    balances[to] += value

    emit Transfer(from: caller, to: to, value: value)

    return true;
  }

  // Approve the passed address to spend the specified amount of tokens on behalf of caller.
  // - param to: The address to transfer to.
  // - param value: The amount to be transferred.
  // - return: true or is reverted
  public func approve(spender: Address, value: Int) -> Bool
    mutates (allowed)
  {
    allowed[caller][spender] = value

    emit Approval(from: caller, to: spender, value: value)
    return true
  }

  // Transfer tokens from one address to another
  // - param from: The address which you want to send tokens from.
  // - param to: The address which you want to transfer to
  // - param value: The amount of tokens to be transferred.
  // - return: true or is reverted

  public func transferFrom(from: Address, to: Address, value: Int) -> Bool
    mutates (allowed, balances)
  {
    let allowance: Int = allowed[from][caller]
    assert(balances[from] >= value && allowance >= value)
    assert(to != 0x00000_00000_00000_00000_00000_00000_00000_00000)

    balances[from] -= value
    balances[to] += value
    allowed[from][caller] -= value

    emit Transfer(from: from, to: to, value: value)
    return true
  }

  // Increase the amount of tokens that an owner allowed to a spender.
  // approve should be called when allowed[_spender] == 0. To increment
  // allowed value is better to use this function to avoid 2 calls (and wait until
  // the first transaction is mined)
  // From MonolithDAO Token.sol
  // - param _spender The address which will spend the funds.
  // - param _addedValue The amount of tokens to increase the allowance by.
  func increaseApproval(spender: Address, addedValue: Int) -> Bool
    mutates (allowed)
  {
    allowed[caller][spender] += addedValue

    emit Approval(from: caller, to: spender, value: allowed[caller][spender])
    return true;
  }

  // Decrease the amount of tokens that an owner allowed to a spender.
  // approve should be called when allowed[_spender] == 0. To decrement
  // allowed value is better to use this func to avoid 2 calls (and wait until
  // the first transaction is mined)
  // From MonolithDAO Token.sol
  // - param _spender The address which will spend the funds.
  // - param _subtractedValue The amount of tokens to decrease the allowance by.
  func decreaseApproval(spender: Address, subtractedValue: Int) -> Bool
    mutates (allowed)
  {
    let oldValue: Int = allowed[caller][spender]
    if subtractedValue >= oldValue {
      allowed[caller][spender] = 0
    }
    else {
      allowed[caller][spender] = oldValue - subtractedValue
    }

    emit Approval(from: caller, to: spender, value: allowed[caller][spender])
    return true;
  }
}

FlintToken @(Minting) :: (owner) {
  // Internal func that mints an amount of the token and assigns it to
  // an account. This encapsulates the modification of balances such that the
  // proper events are emitted.
  // - param account The account that will receive the created tokens.
  // - param amount The amount that will be created.
  public func mint(account: Address, amount: Int) -> Bool
    mutates (totalSupply, balances)
  {
    assert(account != 0x00000_00000_00000_00000_00000_00000_00000_00000);

    totalSupply += amount
    balances[account] += amount

    emit Transfer(to: account, value: amount)
    emit Mint(to: account, value: amount)
    return true
  }

  // Stop minting new tokens
  // Return true if successful
  public func finishMinting() -> Bool {
    emit MintFinished
    return true
    become MintingFinished
  }
}
