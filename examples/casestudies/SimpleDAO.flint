struct Proposal {
  var proposer: Address = 0x0000000000000000000000000000000000000000
  var value: Int = 0
  var recipient: Address = 0x0000000000000000000000000000000000000000
  var yesVotes: Int = 0
  var noVotes: Int = 0
  var quorum: Int = 0
  var finished: Bool = false
  var success: Bool = false
  var voted: [Address: Bool] = [:]
}

contract SimpleDAO (Join, Propose, Vote) {
  var avaliableMemberSlots: Int = 3
  var proposal: Proposal = Proposal()
  var members: [Address] = []
  var balances: [Address: Int] = [:]
  var totalSupply: Int = 0
}

SimpleDAO @(any) :: (any) {
  public init(){
    become Join
  }
}

SimpleDAO @(Propose) :: caller <- (members) {
  public mutating func newProposal(recipient: Address, value: Int) {
    proposal = Proposal()
    proposal.proposer = caller
    proposal.value = value
    proposal.recipient = recipient
    proposal.quorum = totalSupply / 2
    become Vote
  }

  // This isn't a good function because they don't stop being a member when they leave, but it's sufficient for testing
  public mutating func leave() {
    let balance: Int = balances[caller]

    balances[caller] = 0
    totalSupply -= balance
    // Transfer money
  }
}

SimpleDAO @(Vote) :: caller <- (members) {
  public mutating func vote(approve: Bool) {
    if proposal.voted[caller] {
      fatalError()
    }

    if approve {
      proposal.yesVotes += balances[caller]
    } else {
      proposal.noVotes += balances[caller]
    }

    proposal.voted[caller] = true
  }

  public mutating func executeProposal() {
    if(caller != proposal.proposer || proposal.finished) {
      fatalError()
    }

    proposal.finished = true
    if (proposal.yesVotes + proposal.noVotes) >= proposal.quorum && proposal.yesVotes > proposal.noVotes {
       proposal.success = true // Should also transfer money
    }

    become Propose
  }
}

SimpleDAO @(Join) :: caller <- (any) {

  public mutating func join(value: Int) {
    balances[caller] = value
    totalSupply += value
    members[3 - avaliableMemberSlots] = caller

    avaliableMemberSlots -= 1
    if avaliableMemberSlots < 1 {
      become Propose
    }
  }

  public func isJoin() -> Bool {
    return true
  }
}

SimpleDAO @(any) :: (any) {
  public func isMember(candidate: Address) -> Bool {
    var found: Bool = false
    for let member: Address in members {
      if member == candidate {
        found = true
      }
    }
    return found
  }

  public func slotsLeft() -> Int {
    return avaliableMemberSlots
  }
}

